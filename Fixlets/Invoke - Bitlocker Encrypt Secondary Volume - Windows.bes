<?xml version="1.0" encoding="UTF-8"?>

<BES xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="BES.xsd">
	<Fixlet>
		<Title>Invoke - Bitlocker Encrypt Secondary Volume - Windows</Title>
		<Description><![CDATA[<P>This fixlet will encrypt the specified drive of a device.</P>
<P>The recovery password generated by action 1 is available via the <STRONG>Analysis: Bitlocker - Recovery Password - Windows</STRONG></P>
<P>Information on using this content is available here: <A href="https://github.com/strawgate/C3-Protect/wiki/Bitlocker">https://github.com/strawgate/C3-Protect/wiki/Bitlocker</A></P>]]></Description>
		<Relevance>windows of operating system</Relevance>
		<Relevance><![CDATA[(windows of operating system) AND (version of operating system >= "6.0") AND (( exists product info string whose(it ends with "Enterprise" OR it starts with "Enterprise" OR it ends with "Ultimate" OR it ends with "Server Datacenter") of operating system ) OR ( (version of operating system >= "6.2") AND (exists product info string whose(it ends with "Business") of operating system) ))]]></Relevance>
		<Relevance>selects "* from Win32_Tpm" of wmis "root\CIMv2\Security\MicrosoftTpm"</Relevance>
		<Relevance><![CDATA[if(exists WMI "root\CIMv2\Security\MicrosoftVolumeEncryption") then (number of select objects ("* from win32_EncryptableVolume")of WMI "root\CIMv2\Security\MicrosoftVolumeEncryption" >= 1) else (false)]]></Relevance>
		<Category>Disk Encryption</Category>
		<Source>Internal</Source>
		<SourceID></SourceID>
		<SourceReleaseDate>2016-04-29</SourceReleaseDate>
		<SourceSeverity></SourceSeverity>
		<CVENames></CVENames>
		<SANSID></SANSID>
		<Domain>BESC</Domain>
		<DefaultAction ID="Action1">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> to encrypt the specified drive using the TPM and a random recovery password.</PostLink>
			</Description>
			<ActionScript MIMEType="application/x-Fixlet-Windows-Shell"><![CDATA[// Enter your action script here

action parameter query "Volume" with description "Volume to encrypt. Example C: or D:"

// If the action fails at this line then the volume is not encryptable
continue if {exists selects ("* from win32_EncryptableVolume where DriveLetter='" & parameter "Volume" of action & "'")of WMI "root\CIMv2\Security\MicrosoftVolumeEncryption"}

// If the action fails at this line then the volume is already initialized for protection
continue if {not boolean value of selects ("IsVolumeInitializedForProtection from win32_EncryptableVolume where DriveLetter='" & parameter "Volume" of action & "'")of WMI "root\CIMv2\Security\MicrosoftVolumeEncryption"}
action uses wow64 redirection {not x64 of operating system}

delete __createfile
delete enable.bat
delete terminate.bat
delete addrecoverykey.bat

createfile until _end_
manage-bde -on {parameter "Volume" of action} > "{client folder of current site}\Enable.log"
_end_

move __createfile enable.bat

createfile until _end_
manage-bde -off {parameter "Volume" of action} > "{client folder of current site}\Terminate.log
manage-bde -protectors {parameter "Volume" of action} -delete -type -recoverypassword >> "{client folder of current site}\Terminate.log
_end_

move __createfile terminate.bat

createfile until _end_
manage-bde -protectors -add {parameter "Volume" of action} -recoverypassword > "{client folder of current site}\AddRecoveryKey.log
_end_

move __createfile addrecoverykey.bat

wait enable.bat

// If the action dies here there is a bootable CD or DVD in the drive
if { exists lines containing "ERROR: BitLocker detected a bootable CD or DVD on the computer." of file "Enable.log" of client folder of current site} 
	// BitLocker is in inconsistent state -- kill encrytpion
	wait terminate.bat
	continue if false
endif

// If the action dies here there was another error
if { exists lines containing "ERROR" of file "Enable.log"} 
	// BitLocker is in inconsistent state -- kill encrytpion
	wait terminate.bat
	continue if false
endif

wait addrecoverykey.bat

action requires restart]]></ActionScript>
		</DefaultAction>
		<Action ID="Action2">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> to encrypt the specified drive using just the TPM.</PostLink>
			</Description>
			<ActionScript MIMEType="application/x-Fixlet-Windows-Shell"><![CDATA[// Enter your action script here

action parameter query "Volume" with description "Volume to encrypt. Example C: or D:"

// If the action fails at this line then the volume is not encryptable
continue if {exists selects ("* from win32_EncryptableVolume where DriveLetter='" & parameter "Volume" of action & "'")of WMI "root\CIMv2\Security\MicrosoftVolumeEncryption"}

// If the action fails at this line then the volume is already initialized for protection
continue if {not boolean value of selects ("IsVolumeInitializedForProtection from win32_EncryptableVolume where DriveLetter='" & parameter "Volume" of action & "'")of WMI "root\CIMv2\Security\MicrosoftVolumeEncryption"}
action uses wow64 redirection {not x64 of operating system}

delete __createfile
delete enable.bat
delete terminate.bat
delete addrecoverykey.bat

createfile until _end_
manage-bde -on {parameter "Volume" of action} > "{client folder of current site}\Enable.log"
_end_

move __createfile enable.bat

createfile until _end_
manage-bde -off {parameter "Volume" of action} > "{client folder of current site}\Terminate.log
manage-bde -protectors {parameter "Volume" of action} -delete -type -recoverypassword >> "{client folder of current site}\Terminate.log
_end_

move __createfile terminate.bat

wait enable.bat

// If the action dies here there is a bootable CD or DVD in the drive
if { exists lines containing "ERROR: BitLocker detected a bootable CD or DVD on the computer." of file "Enable.log" of client folder of current site} 
	// BitLocker is in inconsistent state -- kill encrytpion
	wait terminate.bat
	continue if false
endif

// If the action dies here there was another error
if { exists lines containing "ERROR" of file "Enable.log"} 
	// BitLocker is in inconsistent state -- kill encrytpion
	wait terminate.bat
	continue if false
endif

action requires restart]]></ActionScript>
		</Action>
	</Fixlet>
</BES>
